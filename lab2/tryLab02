#!/bin/csh -f 

if ( `hostname` == "aloha.home" || `hostname -s` == "lagniappe" ) then
  set lib=/home/pnico/CalPoly/Class/cpe357/now/Asgn/Handin/lib/lab02
  set logprog=echo 
  set longlines=/home/pnico/bin/longlines.pl
else
  set lib=/home/pn-cs357/lib/lab02/
  set logprog=/home/pn-cs357/bin/logit.lab02
  set longlines=/home/pn-cs357/bin/longlines.pl
endif

set testdir=$lib/Tests/inputs
set apptest=$lib/testuniq
set asgn=lab02
set exitstatus = 0
onintr bail

if ( $exitstatus == 0 ) then
  echo ""
  echo "------------------------"
  echo "Looking for source files"
  echo "------------------------"
  echo ""

  set nonomatch
  set src = ( *.[ch] )
  if ( "$src" != '*.[ch]' ) then

    echo "Found: $src"
  else
    echo "***** No C source files found. " 
    echo "***** Are you sure you're in the right directory?"
    echo ""
    set exitstatus = -1
  endif

endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "-----------------------"
  echo "Looking for a Makefile"
  echo "----------------------"
  echo ""

  if ( $exitstatus == 0 ) then
    if ( -f Makefile ) then
      echo "Found: Makefile"
    else if ( -f makefile ) then 
      echo "Found: makefile"
    else 
      echo ""
      echo "***** No Makefile found."
      echo ""
      set exitstatus = -2
    endif
  endif
endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "--------------------------------"
  echo "Checking for long lines (no program should have any lines > 80 chars)"
  echo "--------------------------------"
  echo ""
  if ( ! { $longlines $src } ) then
     set exitstatus=-3
  else
     echo "Line length ok."
  endif
  echo ""
endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "------------------------------------"
  echo "Building the application:" \"make uniq\"
  echo "------------------------------------"
  echo ""

  set cmd = ( make uniq )
  echo "$cmd" 
  if ( ! { $cmd  } ) then
    echo ""
    echo "Unable to compile uniq."
    set exitstatus = -4
  endif
endif

if ( $exitstatus == 0 ) then
  echo ""
  echo "---------------"
  echo "checking for uniq"
  echo "---------------"
  echo ""

  if ( ! -f uniq ) then
    echo "program uniq not found"
    set exitstatus = -5
  endif
endif

set which = 0

if ( $exitstatus == 0 ) then
  echo ""
  echo ""
  echo "--------------------------------"
  echo "Running the application "
  echo "--------------------------------"
  echo ""
  echo ""

   setenv LIB $lib
  if ( -x $apptest ) then
    $apptest
    set exitstatus=$status
  else 
    echo $apptest not found.
  endif
endif

goto done

bail:
  echo "Interrupted."
  set exitstatus=-7

done:

$logprog  $exitstatus $which
exit $exitstatus
